[gd_resource type="Shader" format=3 uid="uid://dtp2h5c0r7tl8"]

[resource]
code = "shader_type spatial;
render_mode cull_front;

uniform sampler2DArray texture_albedo : source_color;
uniform float specular = 0.5;
uniform float metallic = 0.0;
uniform float roughness : hint_range(0,1);
uniform float point_size : hint_range(0,128);
uniform sampler2DArray texture_roughness_metallic_emission : hint_default_white;
uniform vec4 roughness_texture_channel = vec4(1,0,0,0);
uniform vec4 metallic_texture_channel = vec4(0,1,0,0);
uniform vec4 emission_texture_channel = vec4(0,0,1,0);
uniform vec4 emission : source_color;
uniform float emission_energy;
uniform sampler2DArray texture_normal : hint_normal;
uniform float normal_scale : hint_range(-16,16) = 1;
uniform vec3 uv1_scale = vec3(1,1,1);
uniform vec3 uv1_offset;
uniform vec3 uv2_scale = vec3(1,1,1);
uniform vec3 uv2_offset;

uniform bool lights_on = false;
uniform bool window_shading = true;

varying vec3 worldpos;


void vertex() {
	UV=UV*uv1_scale.xy+uv1_offset.xy;
	worldpos = (MODEL_MATRIX * vec4(0.0, 0.0, 0.0, 1.0)).xyz;
}

float rand(vec3 co){
    return fract(sin(dot(co.xyz ,vec3(12.9898,78.233,54.1463))) * 43758.5453);
}

void fragment() {
	// Read from the alpha channel the index (depth) of the sampler2Darray
	float depth = COLOR.a * 255.0;
	vec3 base_uv = vec3(UV, depth);
	vec4 albedo_tex = texture(texture_albedo, base_uv);
	
	float metallic_tex = dot(
		texture(texture_roughness_metallic_emission,base_uv),metallic_texture_channel);
	METALLIC = metallic_tex * metallic;
	float roughness_tex = dot(
		texture(texture_roughness_metallic_emission,base_uv),roughness_texture_channel);
	ROUGHNESS = roughness_tex * roughness;
	float emission_tex = dot(
		texture(texture_roughness_metallic_emission,base_uv),emission_texture_channel);
	
	// Modulate color with vertex color - this is more performant than a color uniform
	// Do not modulate glasses (i.e. where metallic is 1)
	if(emission_tex < 0.5)
		ALBEDO = COLOR.rgb * albedo_tex.rgb;
	else
		ALBEDO = albedo_tex.rgb;
	
	SPECULAR = specular;
	NORMAL_MAP = texture(texture_normal,base_uv).rgb;
	NORMAL_MAP_DEPTH = normal_scale;
	
	if (lights_on) {
		int random_seed = int(rand(round(worldpos)) * rand(vec3(floor(UV), 0.0)) * 100000.0);
		bool light_here = random_seed % 3 == 2;
		if (light_here) {
			EMISSION = (emission.rgb*emission_tex) * (emission_energy * (0.1 + fract(float(int(floor(UV.x)) * random_seed) / 1000.0) * 0.9));
		}
	}
}"
